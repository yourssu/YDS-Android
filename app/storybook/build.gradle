plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def versionProperties = new Properties()
versionProperties.load(new FileInputStream(file("../../version.properties")))

android {
    compileSdk 33

    // 당연하지만 배포용은 아니고 release 테스트용
    signingConfigs {
        release {
            storeFile file("./ydsyourssu.jks")
            storePassword (System.getenv("STORE_PASSWORD"))
            keyAlias (System.getenv("KEY_ALIAS"))
            keyPassword (System.getenv("KEY_PASSWORD"))
        }
    }

    defaultConfig {
        applicationId "com.yourssu.storybook"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode (System.getenv("BUILD_NUMBER") as Integer ?: 10000) // 로컬에서 10000 번으로 빌드됨
        versionName versionProperties['versionName']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "YDS-StoryBook-" + versionProperties['versionName'] + "(" + versionCode + ")")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "apk_type"

    productFlavors {
        dev {
            dimension "apk_type"
            applicationIdSuffix ".dev"

            buildConfigField 'boolean', 'DEV', 'true'
        }

        production {
            dimension "apk_type"

            buildConfigField 'boolean', 'DEV', 'false'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.3"
    }
    buildFeatures {
        dataBinding = true
        compose = true
    }
}

dependencies {
    implementation project(path: ':DesignSystem')
    implementation project(path: ':compose')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'

    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.compose.material:material:1.4.0"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}